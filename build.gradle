buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        gradlePluginPortal()
        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
        maven { url "https://artifactory.nimblygames.com/artifactory/ng-public"}
        google()
    }
    dependencies {
        classpath 'org.docstr:gwt-gradle-plugin:1.1.30'
        classpath 'org.gretty:gretty:3.1.0'
        classpath 'com.android.tools.build:gradle:8.0.2'
        classpath 'com.badlogicgames.packr:packr:4.1.0-SNAPSHOT'
        classpath 'de.undercouch:gradle-download-task:5.5.0'
    }
}

plugins {
    id 'java-library'
    id 'jacoco'
    id 'org.sonarqube' version '4.4.1.3373'
    id 'com.adarshr.test-logger' version '4.0.0'
    id 'org.beryx.jlink' version '3.0.1'
}

def JDK_VERSION = 17
java.toolchain.languageVersion.set(JavaLanguageVersion.of(JDK_VERSION))

allprojects {
    apply plugin: "eclipse"

    version = '0.1'
    ext {
        appName = 'brozone'
        gdxVersion = '1.12.2-SNAPSHOT'
        roboVMVersion = '2.3.20'
        box2DLightsVersion = '1.5'
        gdxControllersVersion = '2.2.1'
        activeJdkCache = true

        getPackage = {
            return project(":desktop").ext.mainClassName.substring(0, project(":desktop").ext.mainClassName.indexOf("Desktop") - 1)
        }

        getNeatVersionString = {
            return "${appName.toLowerCase()}-v$version"
        }

        generateDesktopReleaseName = { String platform ->

            if(platform.endsWith("64")){
                platform = "${platform.substring(0, platform.length() - 2)}"
            }

            return "${getNeatVersionString()}_${platform.toString().toLowerCase()}"
        }

        generateJdkUrl = { String platform ->
            return "https://api.adoptium.net/v3/binary/latest/${JDK_VERSION}/ga/${platform.toLowerCase().replace("64", "").replace("os", "")}/x64/jdk/hotspot/normal/eclipse?project=jdk"
        }
    }

    repositories {
        mavenLocal()
        mavenCentral()
        google()
        gradlePluginPortal()
        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
        maven { url "https://oss.sonatype.org/content/repositories/releases/" }
        maven { url "https://jitpack.io" }
        maven { url "https://artifactory.nimblygames.com/artifactory/ng-public"}
    }
}

sonar {
    properties {
        property 'sonar.host.url', 'https://sonarcloud.io'
        property 'sonar.organization', 'vmillet-dev'
        property 'sonar.projectKey', 'vmillet-dev_Brozone'
        property 'sonar.exclusions', '**/*.java'
    }
}