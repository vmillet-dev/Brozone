buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        gradlePluginPortal()
        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
        maven { url "https://artifactory.nimblygames.com/artifactory/ng-public"}
        google()
    }
    dependencies {
        classpath 'org.docstr:gwt-gradle-plugin:1.1.30'
        classpath 'org.gretty:gretty:3.1.0'
        classpath 'com.badlogicgames.packr:packr:4.1.0-SNAPSHOT'
    }
}

plugins {
    id("java-library")
}

java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(17))
    }
}

allprojects {
    apply plugin: "eclipse"
    apply plugin: "java-library"

    version = '1.0'
    ext {
        appName = "brozone"
        gdxVersion = '1.12.2-SNAPSHOT'
        roboVMVersion = '2.3.20'
        box2DLightsVersion = '1.5'
        ashleyVersion = '1.7.4'
        aiVersion = '1.8.2'
        gdxControllersVersion = '2.2.1'

        getPackage = {
            return project(":desktop").ext.mainClassName.substring(0, project(":desktop").ext.mainClassName.indexOf("Desktop") - 1)
        }

        getNeatVersionString = {
            return "v$version"
        }

        generateDeployName = { String platform ->
            if(platform == "windows"){
                platform += "64"
            }
            platform = platform.capitalize()

            if(platform.endsWith("64") || platform.endsWith("32")){
                platform = "${platform.substring(0, platform.length() - 2)}-${platform.substring(platform.length() - 2)}bit"
            }

            return "[${platform}][${getNeatVersionString()}]${appName}"
        }
    }

    repositories {
        mavenLocal()
        mavenCentral()
        google()
        gradlePluginPortal()
        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
        maven { url "https://oss.sonatype.org/content/repositories/releases/" }
        maven { url "https://jitpack.io" }
        maven { url "https://artifactory.nimblygames.com/artifactory/ng-public"}
    }
}

project(":core") {
    dependencies {
        api "com.badlogicgames.gdx:gdx:$gdxVersion"

        testImplementation 'org.junit.jupiter:junit-jupiter-engine:5.10.1'
        testImplementation "org.junit.jupiter:junit-jupiter:5.9.2"
        testRuntimeOnly "org.junit.platform:junit-platform-launcher"

        testImplementation 'org.mockito:mockito-junit-jupiter:5.8.0'
        testImplementation "com.badlogicgames.gdx:gdx-backend-headless:$gdxVersion"
        testImplementation "com.badlogicgames.gdx:gdx:$gdxVersion"
        testImplementation "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-desktop"
    }
}

project(":desktop") {
    dependencies {
        implementation project(":core")

        api "com.badlogicgames.gdx:gdx-backend-lwjgl3:$gdxVersion"
        api "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-desktop"
    }
}

project(":html") {
    apply plugin: "gwt"
    apply plugin: "war"
    apply plugin: "org.gretty"

    dependencies {
        implementation project(":core")

        api "com.badlogicgames.gdx:gdx-backend-gwt:$gdxVersion"
        api "com.badlogicgames.gdx:gdx:$gdxVersion:sources"
        api "com.badlogicgames.gdx:gdx-backend-gwt:$gdxVersion:sources"
        api "com.google.jsinterop:jsinterop-annotations:2.0.2:sources"
    }
}