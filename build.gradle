buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        gradlePluginPortal()
        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
        maven { url "https://artifactory.nimblygames.com/artifactory/ng-public"}
        google()
    }
    dependencies {
        classpath 'org.docstr:gwt-gradle-plugin:1.1.30'
        classpath 'org.gretty:gretty:3.1.0'
        classpath 'com.android.tools.build:gradle:8.0.2'
        classpath 'com.badlogicgames.packr:packr:4.1.0-SNAPSHOT'
        classpath 'de.undercouch:gradle-download-task:5.5.0'
    }
}

plugins {
    id "java-library"
    id "org.sonarqube" version "4.4.1.3373"
}

def JDK_VERSION = 17

java.toolchain.languageVersion.set(JavaLanguageVersion.of(JDK_VERSION))

allprojects {
    apply plugin: "eclipse"

    version = '0.1'
    ext {
        appName = 'brozone'
        gdxVersion = '1.12.2-SNAPSHOT'
        roboVMVersion = '2.3.20'
        box2DLightsVersion = '1.5'
        gdxControllersVersion = '2.2.1'
        activeJdkCache = true

        getPackage = {
            return project(":desktop").ext.mainClassName.substring(0, project(":desktop").ext.mainClassName.indexOf("Desktop") - 1)
        }

        getNeatVersionString = {
            return "${appName}-v$version"
        }

        generateDesktopReleaseName = { String platform ->

            if(platform.endsWith("64")){
                platform = "${platform.substring(0, platform.length() - 2)}"
            }

            return "${getNeatVersionString()}_${platform.toString().toLowerCase()}"
        }

        urlJdkWindows = 'https://download.java.net/java/GA/jdk17.0.2/dfd4a8d0985749f896bed50d7138ee7f/8/GPL/openjdk-17.0.2_windows-x64_bin.zip'
        urlJdkMacOs = 'https://download.java.net/java/GA/jdk17.0.2/dfd4a8d0985749f896bed50d7138ee7f/8/GPL/openjdk-17.0.2_macos-x64_bin.tar.gz'
        urlJdkLinux = 'https://download.java.net/java/GA/jdk17.0.2/dfd4a8d0985749f896bed50d7138ee7f/8/GPL/openjdk-17.0.2_linux-x64_bin.tar.gz'
    }

    repositories {
        mavenLocal()
        mavenCentral()
        google()
        gradlePluginPortal()
        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
        maven { url "https://oss.sonatype.org/content/repositories/releases/" }
        maven { url "https://jitpack.io" }
        maven { url "https://artifactory.nimblygames.com/artifactory/ng-public"}
    }
}

sonar {
    properties {
        property 'sonar.host.url', 'https://sonarcloud.io'
        property 'sonar.organization', 'vmillet-dev'
        property 'sonar.projectKey', 'vmillet-dev_Brozone'
        property 'sonar.coverage.jacoco.xmlReportPaths', "${project.projectDir.toPath()}/core/build/reports/jacoco/test/jacocoTestReport.xml"
    }
}

project(":core") {
    apply plugin: "java-library"

    dependencies {
        api "com.badlogicgames.gdx:gdx:$gdxVersion"

        testImplementation "org.assertj:assertj-core:3.6.1"
        testImplementation "org.junit.jupiter:junit-jupiter-engine:5.10.1"
        testImplementation "org.junit.jupiter:junit-jupiter:5.10.1"
        testRuntimeOnly "org.junit.platform:junit-platform-launcher"

        testImplementation "org.mockito:mockito-junit-jupiter:5.8.0"
        testImplementation "com.badlogicgames.gdx:gdx-backend-headless:$gdxVersion"
        testImplementation "com.badlogicgames.gdx:gdx:$gdxVersion"
        testImplementation "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-desktop"
    }

    sonar {
        properties {
            property "sonar.sources", "src/main/java"
            property "sonar.coverage.jacoco.xmlReportPaths", "$projectDir.parentFile.path/build/reports/jacoco/codeCoverageReport/codeCoverageReport.xml"
        }
    }
}

project(":desktop") {
    apply plugin: "java-library"

    dependencies {
        implementation project(":core")

        api "com.badlogicgames.gdx:gdx-backend-lwjgl3:$gdxVersion"
        api "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-desktop"
    }

    sonar {
        skipProject true
    }
}

project(":html") {
    apply plugin: "gwt"
    apply plugin: "war"
    apply plugin: "org.gretty"
    apply plugin: "java-library"

    dependencies {
        implementation project(":core")

        api "com.badlogicgames.gdx:gdx-backend-gwt:$gdxVersion"
        api "com.badlogicgames.gdx:gdx:$gdxVersion:sources"
        api "com.badlogicgames.gdx:gdx-backend-gwt:$gdxVersion:sources"
        api "com.google.jsinterop:jsinterop-annotations:2.0.2:sources"
    }

    sonar {
        skipProject true
    }
}

project(":android") {
    apply plugin: "com.android.application"

    configurations { natives }

    dependencies {
        implementation project(":core")
        api "com.badlogicgames.gdx:gdx-backend-android:$gdxVersion"
        natives "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-armeabi-v7a"
        natives "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-arm64-v8a"
        natives "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-x86"
        natives "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-x86_64"

    }

    sonar {
        skipProject true
    }
}